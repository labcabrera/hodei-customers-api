plugins {
	id 'org.springframework.boot' version '2.3.4.RELEASE'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'com.palantir.docker' version '0.25.0'
	id 'java'
}

group = 'org.github.labcabrera'
version = version
sourceCompatibility = '11'

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}


repositories {
	mavenLocal()
	mavenCentral()
	maven {
		url = uri("https://maven.pkg.github.com/labcabrera/hodei-starter-rsql")
		credentials {
			username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
			password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
		}
	}
	maven {
		url = uri("https://maven.pkg.github.com/labcabrera/hodei-starter-security-jwt")
		credentials {
			username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
			password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
		}
	}
}

ext {
	set('springCloudVersion', "Hoxton.SR8")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-cache'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.cloud:spring-cloud-stream-binder-rabbit'
	
	implementation 'org.springdoc:springdoc-openapi-ui:1.4.8'
	implementation 'org.springdoc:springdoc-openapi-data-rest:1.4.8'
	
	// Note starter-jwt provides "runtime" jsonwebtoken, but for testing purpose we included a dummy JWT
	// tokens in OpenApi config and we require "compile" dependency
	implementation 'io.jsonwebtoken:jjwt:0.9.1'
	
	implementation 'com.github.labcabrera:hodei-model-commons:1.0.0-SNAPSHOT'
	implementation 'com.github.labcabrera:hodei-starter-security-jwt:1.0.0-SNAPSHOT'
	implementation 'com.github.labcabrera:hodei-starter-rsql:1.0.0-SNAPSHOT'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'org.springframework.cloud:spring-cloud-stream-test-support'
	testImplementation 'org.springframework.cloud:spring-cloud-stream-binder-test'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

test {
	useJUnitPlatform()
}

docker {
	name "labcabrera/${jar.baseName}"
	tags 'latest'
	dockerfile file('Docker/Dockerfile')
	files jar.archivePath
	buildArgs(['JAR_FILE': "${jar.archiveName}"])
}
